# Copyright Amazon.com, Inc. or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0
#
# Lambda Functions for Bedrock LLM Processor

# OCR Function
resource "aws_lambda_function" "ocr" {
  architectures = [var.lambda_architecture]
  function_name = "${local.name_prefix}-ocr"
  role          = aws_iam_role.ocr_lambda.arn
  handler       = "index.handler"
  runtime       = "python3.12"
  timeout       = 900
  memory_size   = 1024

  filename         = data.archive_file.ocr_lambda.output_path
  source_code_hash = data.archive_file.ocr_lambda.output_base64sha256

  layers = [var.idp_common_layer_arn != null ? var.idp_common_layer_arn : var.base_layer_arn]

  kms_key_arn = var.encryption_key_arn

  environment {
    variables = {
      LOG_LEVEL                = local.log_level
      METRIC_NAMESPACE         = local.metric_namespace
      MAX_WORKERS              = var.ocr_max_workers
      CONFIGURATION_TABLE_NAME = local.configuration_table_name
      TRACKING_TABLE           = local.tracking_table_name
      WORKING_BUCKET           = local.working_bucket_name
      DOCUMENT_TRACKING_MODE   = local.api_id != null ? "appsync" : "dynamodb"
      APPSYNC_API_URL          = local.api_graphql_url != null ? local.api_graphql_url : ""
      # ARN of the deployment-scoped BDA OCR project for `ocr.backend: bda`
      # (IDP v0.6). Empty unless var.enable_bda_ocr_backend is set — see
      # bda_ocr_project.tf — and the `bda` backend then errors clearly.
      BDA_OCR_PROJECT_ARN = local.bda_ocr_project_arn
    }
  }

  dynamic "vpc_config" {
    for_each = length(local.vpc_subnet_ids) > 0 ? [1] : []
    content {
      subnet_ids         = local.vpc_subnet_ids
      security_group_ids = local.vpc_security_group_ids
    }
  }

  tracing_config {
    mode = var.lambda_tracing_mode
  }

  tags = local.common_tags
}

# Classification Function
resource "aws_lambda_function" "classification" {
  architectures = [var.lambda_architecture]
  function_name = "${local.name_prefix}-classification"
  role          = aws_iam_role.classification_lambda.arn
  handler       = "index.handler"
  runtime       = "python3.12"
  timeout       = 900
  memory_size   = 3008

  filename         = data.archive_file.classification_lambda.output_path
  source_code_hash = data.archive_file.classification_lambda.output_base64sha256

  layers = [var.idp_common_layer_arn != null ? var.idp_common_layer_arn : var.base_layer_arn]

  kms_key_arn = var.encryption_key_arn

  environment {
    variables = merge({
      METRIC_NAMESPACE         = local.metric_namespace
      MAX_WORKERS              = var.classification_max_workers
      TRACKING_TABLE           = local.tracking_table_name
      CONFIGURATION_TABLE_NAME = local.configuration_table_name
      LOG_LEVEL                = local.log_level
      WORKING_BUCKET           = local.working_bucket_name
      GUARDRAIL_ID_AND_VERSION = var.classification_guardrail != null ? var.classification_guardrail.guardrail_id : ""
      DOCUMENT_TRACKING_MODE   = local.api_id != null ? "appsync" : "dynamodb"
      APPSYNC_API_URL          = local.api_graphql_url != null ? local.api_graphql_url : ""
      },
      var.classification_backend == "sagemaker" ? {
        SAGEMAKER_ENDPOINT_NAME = var.classification_sagemaker_endpoint_arn != null ? element(split("/", var.classification_sagemaker_endpoint_arn), 1) : ""
      } : {},
      local.bedrock_assume_role_env
    )
  }

  dynamic "vpc_config" {
    for_each = length(local.vpc_subnet_ids) > 0 ? [1] : []
    content {
      subnet_ids         = local.vpc_subnet_ids
      security_group_ids = local.vpc_security_group_ids
    }
  }

  tracing_config {
    mode = var.lambda_tracing_mode
  }

  tags = local.common_tags
}

# Extraction Function
resource "aws_lambda_function" "extraction" {
  architectures = [var.lambda_architecture]
  function_name = "${local.name_prefix}-extraction"
  role          = aws_iam_role.extraction_lambda.arn
  handler       = "index.handler"
  runtime       = "python3.12"
  timeout       = 900
  memory_size   = 1024

  filename         = data.archive_file.extraction_lambda.output_path
  source_code_hash = data.archive_file.extraction_lambda.output_base64sha256

  layers = [var.idp_common_layer_arn != null ? var.idp_common_layer_arn : var.base_layer_arn]

  kms_key_arn = var.encryption_key_arn

  environment {
    variables = merge({
      METRIC_NAMESPACE         = local.metric_namespace
      CONFIGURATION_TABLE_NAME = local.configuration_table_name
      WORKING_BUCKET           = local.working_bucket_name
      GUARDRAIL_ID_AND_VERSION = var.extraction_guardrail != null ? var.extraction_guardrail.guardrail_id : ""
      LOG_LEVEL                = local.log_level
      TRACKING_TABLE           = local.tracking_table_name
      DOCUMENT_TRACKING_MODE   = local.api_id != null ? "appsync" : "dynamodb"
      APPSYNC_API_URL          = local.api_graphql_url != null ? local.api_graphql_url : ""
    }, local.bedrock_assume_role_env)
  }

  dynamic "vpc_config" {
    for_each = length(local.vpc_subnet_ids) > 0 ? [1] : []
    content {
      subnet_ids         = local.vpc_subnet_ids
      security_group_ids = local.vpc_security_group_ids
    }
  }

  tracing_config {
    mode = var.lambda_tracing_mode
  }

  tags = local.common_tags
}

# Process Results Function
resource "aws_lambda_function" "process_results" {
  architectures = [var.lambda_architecture]
  function_name = "${local.name_prefix}-process-results"
  role          = aws_iam_role.process_results_lambda.arn
  handler       = "index.handler"
  runtime       = "python3.12"
  timeout       = 900
  memory_size   = 1024

  filename         = data.archive_file.process_results_lambda.output_path
  source_code_hash = data.archive_file.process_results_lambda.output_base64sha256

  layers = [var.idp_common_layer_arn != null ? var.idp_common_layer_arn : var.base_layer_arn]

  kms_key_arn = var.encryption_key_arn

  environment {
    variables = {
      METRIC_NAMESPACE         = local.metric_namespace
      LOG_LEVEL                = local.log_level
      TRACKING_TABLE           = local.tracking_table_name
      CONFIGURATION_TABLE_NAME = local.configuration_table_name
      WORKING_BUCKET           = local.working_bucket_name
      OUTPUT_BUCKET            = local.output_bucket_name
      DOCUMENT_TRACKING_MODE   = local.api_id != null ? "appsync" : "dynamodb"
      APPSYNC_API_URL          = local.api_graphql_url != null ? local.api_graphql_url : ""
    }
  }

  dynamic "vpc_config" {
    for_each = length(local.vpc_subnet_ids) > 0 ? [1] : []
    content {
      subnet_ids         = local.vpc_subnet_ids
      security_group_ids = local.vpc_security_group_ids
    }
  }

  tracing_config {
    mode = var.lambda_tracing_mode
  }

  tags = local.common_tags
}

# Summarization Function (conditional)
resource "aws_lambda_function" "summarization" {
  architectures = [var.lambda_architecture]
  count         = var.is_summarization_enabled ? 1 : 0

  function_name = "${local.name_prefix}-summarization"
  role          = aws_iam_role.summarization_lambda[0].arn
  handler       = "index.handler"
  runtime       = "python3.12"
  timeout       = 900
  memory_size   = 1024

  filename         = data.archive_file.summarization_lambda.output_path
  source_code_hash = data.archive_file.summarization_lambda.output_base64sha256

  layers = [var.idp_common_layer_arn != null ? var.idp_common_layer_arn : var.base_layer_arn]

  kms_key_arn = var.encryption_key_arn

  environment {
    variables = merge({
      METRIC_NAMESPACE         = local.metric_namespace
      CONFIGURATION_TABLE_NAME = local.configuration_table_name
      WORKING_BUCKET           = local.working_bucket_name
      LOG_LEVEL                = local.log_level
      GUARDRAIL_ID_AND_VERSION = var.summarization_guardrail != null ? var.summarization_guardrail.guardrail_id : ""
      TRACKING_TABLE           = local.tracking_table_name
      DOCUMENT_TRACKING_MODE   = local.api_id != null ? "appsync" : "dynamodb"
      APPSYNC_API_URL          = local.api_graphql_url != null ? local.api_graphql_url : ""
    }, local.bedrock_assume_role_env)
  }

  dynamic "vpc_config" {
    for_each = length(local.vpc_subnet_ids) > 0 ? [1] : []
    content {
      subnet_ids         = local.vpc_subnet_ids
      security_group_ids = local.vpc_security_group_ids
    }
  }

  tracing_config {
    mode = var.lambda_tracing_mode
  }

  tags = local.common_tags
}

# CloudWatch Log Groups for Lambda Functions
resource "aws_cloudwatch_log_group" "ocr_lambda" {
  name              = "/aws/lambda/${aws_lambda_function.ocr.function_name}"
  retention_in_days = local.log_retention_days
  kms_key_id        = local.encryption_key_arn

  tags = local.common_tags
}

resource "aws_cloudwatch_log_group" "classification_lambda" {
  name              = "/aws/lambda/${aws_lambda_function.classification.function_name}"
  retention_in_days = local.log_retention_days
  kms_key_id        = local.encryption_key_arn

  tags = local.common_tags
}

resource "aws_cloudwatch_log_group" "extraction_lambda" {
  name              = "/aws/lambda/${aws_lambda_function.extraction.function_name}"
  retention_in_days = local.log_retention_days
  kms_key_id        = local.encryption_key_arn

  tags = local.common_tags
}

resource "aws_cloudwatch_log_group" "process_results_lambda" {
  name              = "/aws/lambda/${aws_lambda_function.process_results.function_name}"
  retention_in_days = local.log_retention_days
  kms_key_id        = local.encryption_key_arn

  tags = local.common_tags
}

resource "aws_cloudwatch_log_group" "summarization_lambda" {
  count = var.is_summarization_enabled ? 1 : 0

  name              = "/aws/lambda/${aws_lambda_function.summarization[0].function_name}"
  retention_in_days = local.log_retention_days
  kms_key_id        = local.encryption_key_arn

  tags = local.common_tags
}

# Assessment Function (always deployed, controlled by configuration)
resource "aws_lambda_function" "assessment" {
  architectures = [var.lambda_architecture]
  function_name = "${local.name_prefix}-assessment"
  role          = aws_iam_role.assessment_lambda.arn
  handler       = "index.handler"
  runtime       = "python3.12"
  timeout       = 900
  memory_size   = 512

  filename         = data.archive_file.assessment_lambda.output_path
  source_code_hash = data.archive_file.assessment_lambda.output_base64sha256

  layers = [var.idp_common_layer_arn != null ? var.idp_common_layer_arn : var.base_layer_arn]

  kms_key_arn = var.encryption_key_arn

  environment {
    variables = merge({
      METRIC_NAMESPACE         = local.metric_namespace
      CONFIGURATION_TABLE_NAME = local.configuration_table_name
      LOG_LEVEL                = local.log_level
      WORKING_BUCKET           = local.working_bucket_name
      TRACKING_TABLE           = local.tracking_table_name
      DOCUMENT_TRACKING_MODE   = local.api_id != null ? "appsync" : "dynamodb"
      APPSYNC_API_URL          = local.api_graphql_url != null ? local.api_graphql_url : ""
    }, local.bedrock_assume_role_env)
  }

  dynamic "vpc_config" {
    for_each = length(local.vpc_subnet_ids) > 0 ? [1] : []
    content {
      subnet_ids         = local.vpc_subnet_ids
      security_group_ids = local.vpc_security_group_ids
    }
  }

  tracing_config {
    mode = var.lambda_tracing_mode
  }

  tags = local.common_tags
}

resource "aws_cloudwatch_log_group" "assessment_lambda" {
  name              = "/aws/lambda/${aws_lambda_function.assessment.function_name}"
  retention_in_days = local.log_retention_days
  kms_key_id        = local.encryption_key_arn

  tags = local.common_tags
}

# Create module-specific build directory
resource "null_resource" "create_module_build_dir" {
  provisioner "local-exec" {
    command = "mkdir -p ${local.module_build_dir}"
  }
}

# Generate unique build ID for this module instance
resource "random_id" "build_id" {
  byte_length = 8
  keepers = {
    # Include module instance ID for uniqueness
    module_instance_id = local.module_instance_id
    # Trigger rebuild when content changes
    content_hash = md5("unified-processor-lambda")
  }
}

data "archive_file" "assessment_lambda" {
  type        = "zip"
  source_dir  = "${path.module}/../../../sources/patterns/unified/src/assessment_function"
  output_path = "${path.module}/assessment_function.zip"

  depends_on = [null_resource.create_module_build_dir]
}

data "archive_file" "ocr_lambda" {
  type        = "zip"
  source_dir  = "${path.module}/../../../sources/patterns/unified/src/ocr_function"
  output_path = "${path.module}/ocr_function.zip"

  depends_on = [null_resource.create_module_build_dir]
}

data "archive_file" "classification_lambda" {
  type        = "zip"
  source_dir  = var.classification_backend == "sagemaker" ? "${path.module}/src/sagemaker_classification_function" : "${path.module}/../../../sources/patterns/unified/src/classification_function"
  output_path = "${path.module}/classification_function.zip"

  depends_on = [null_resource.create_module_build_dir]
}

data "archive_file" "extraction_lambda" {
  type        = "zip"
  source_dir  = "${path.module}/../../../sources/patterns/unified/src/extraction_function"
  output_path = "${path.module}/extraction_function.zip"

  depends_on = [null_resource.create_module_build_dir]
}

data "archive_file" "process_results_lambda" {
  type        = "zip"
  source_dir  = "${path.module}/../../../sources/patterns/unified/src/processresults_function"
  output_path = "${path.module}/process_results_function.zip"

  depends_on = [null_resource.create_module_build_dir]
}

data "archive_file" "summarization_lambda" {
  type        = "zip"
  source_dir  = "${path.module}/../../../sources/patterns/unified/src/summarization_function"
  output_path = "${path.module}/summarization_function.zip"

  depends_on = [null_resource.create_module_build_dir]
}

# Evaluation Function (conditional on evaluation_enabled and baseline bucket)
data "archive_file" "evaluation_lambda" {
  count = var.evaluation_enabled ? 1 : 0

  type        = "zip"
  source_dir  = "${path.module}/../../../sources/patterns/unified/src/evaluation_function"
  output_path = "${path.module}/evaluation_function.zip"

  depends_on = [null_resource.create_module_build_dir]
}

resource "aws_lambda_function" "evaluation_function" {
  architectures = [var.lambda_architecture]
  count         = var.evaluation_enabled ? 1 : 0

  function_name = "${local.name_prefix}-evaluation"
  role          = aws_iam_role.evaluation_lambda[0].arn
  handler       = "index.handler"
  runtime       = "python3.12"
  timeout       = 900
  memory_size   = 1024

  filename         = data.archive_file.evaluation_lambda[0].output_path
  source_code_hash = data.archive_file.evaluation_lambda[0].output_base64sha256

  # Evaluation needs the dedicated evaluation+docs_service layer (munkres/numpy
  # for the Hungarian-algorithm comparator), falling back to idp_common then
  # base layer.
  layers = [
    coalesce(
      var.evaluation_layer_arn,
      var.idp_common_layer_arn,
      var.base_layer_arn,
    )
  ]

  kms_key_arn = var.encryption_key_arn

  environment {
    variables = merge({
      LOG_LEVEL                = local.log_level
      METRIC_NAMESPACE         = local.metric_namespace
      TRACKING_TABLE           = local.tracking_table_name
      CONFIGURATION_TABLE_NAME = local.configuration_table_name
      PROCESSING_OUTPUT_BUCKET = local.output_bucket_name
      EVALUATION_OUTPUT_BUCKET = local.output_bucket_name
      BASELINE_BUCKET          = element(split(":", var.evaluation_baseline_bucket_arn), 5)
      WORKING_BUCKET           = local.working_bucket_name
      DOCUMENT_TRACKING_MODE   = local.api_id != null ? "appsync" : "dynamodb"
      APPSYNC_API_URL          = local.api_graphql_url != null ? local.api_graphql_url : ""
    }, local.bedrock_assume_role_env, local.evaluation_reporting_env)
  }

  dynamic "vpc_config" {
    for_each = length(local.vpc_subnet_ids) > 0 ? [1] : []
    content {
      subnet_ids         = local.vpc_subnet_ids
      security_group_ids = local.vpc_security_group_ids
    }
  }

  tracing_config {
    mode = var.lambda_tracing_mode
  }

  tags = local.common_tags
}

resource "aws_cloudwatch_log_group" "evaluation_lambda" {
  count = var.evaluation_enabled ? 1 : 0

  name              = "/aws/lambda/${aws_lambda_function.evaluation_function[0].function_name}"
  retention_in_days = local.log_retention_days
  kms_key_id        = local.encryption_key_arn

  tags = local.common_tags
}
